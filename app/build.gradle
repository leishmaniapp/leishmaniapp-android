plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

ext {
    androidTargetSdk = 34
}

android {
    namespace 'com.leishmaniapp'
    compileSdk androidTargetSdk
    compileSdkPreview "UpsideDownCakePrivacySandbox"

    defaultConfig {
        applicationId "com.leishmaniapp"
        minSdk 27
        targetSdk androidTargetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                '-opt-in=androidx.compose.material3.ExperimentalMaterial3Api',
                '-opt-in=androidx.compose.foundation.ExperimentalFoundationApi'
        ]
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'

    // Android core dependencies
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')

    // Jetpack Compose
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    // TODO: Change to next stable release of material3 when possible
    implementation 'androidx.compose.material3:material3-android:1.2.0-alpha08'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.activity:activity-compose:1.8.0'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose'

    // Google Icons for Jetpack Compose
    implementation 'androidx.compose.material:material-icons-core'
    implementation 'androidx.compose.material:material-icons-extended'

    // Jetpack Compose Testing
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

    // Jetpack Compose Previews
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // Jetpack Compose Navigation
    implementation 'androidx.navigation:navigation-compose:2.7.4'

    // Other Jetpack libraries
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.48'
    kapt "com.google.dagger:hilt-compiler:2.48"

    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.48'
    kaptAndroidTest 'com.google.dagger:hilt-compiler:2.48'

    testImplementation 'com.google.dagger:hilt-android-testing:2.48'
    kaptTest 'com.google.dagger:hilt-compiler:2.48'

    // Kotlinx extensions
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.8.22"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.4.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"

    // Functional programming with Arrow library
    implementation "io.arrow-kt:arrow-core:1.2.0"
    implementation "io.arrow-kt:arrow-fx-coroutines:1.2.0"

    // Mock data generator
    debugImplementation 'io.bloco:faker:2.0.2'

    // Security
    implementation 'org.mindrot:jbcrypt:0.4'

    // CameraX
    implementation "androidx.camera:camera-camera2:1.2.3"
    implementation "androidx.camera:camera-lifecycle:1.2.3"
    implementation "androidx.camera:camera-view:1.2.3"

    // Room Database
    implementation "androidx.room:room-runtime:2.5.2"
    implementation "androidx.room:room-ktx:2.5.2"
    androidTestImplementation "androidx.room:room-testing:2.5.2"
    ksp "androidx.room:room-compiler:2.5.2"

    // Coil
    implementation "io.coil-kt:coil:2.4.0"
    implementation("io.coil-kt:coil-compose:2.4.0")
}

kapt {
    correctErrorTypes true
}

class RoomSchemaArgProvider implements CommandLineArgumentProvider {

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    File schemaDir

    RoomSchemaArgProvider(File schemaDir) {
        this.schemaDir = schemaDir
    }

    @Override
    Iterable<String> asArguments() {
        return ["room.schemaLocation=${schemaDir.path}".toString()]
    }
}

ksp {
    arg(new RoomSchemaArgProvider(new File(projectDir, "schemas")))
}